plugins {
    id 'fabric-loom'
    id 'java'
    id 'maven-publish'
    id 'net.darkhax.curseforgegradle'
    id 'com.modrinth.minotaur'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

base {
    archivesName = "${mod_archives_name}-fabric-${minecraft_version}-${mod_version}"
}

repositories {
    mavenCentral()
    maven {
        name = 'CurseMaven'
        url = 'https://www.cursemaven.com'
    }
    flatDir { dirs 'libs' }
}

def commonSrcDir = "${project.buildDir}/classes/generated/main"
def changelogText = rootProject.file('changelog.md').text

project.delete(fileTree(dir: commonSrcDir))

project(':common') {
    task replaceFabricAnnotations(type: Copy) {
        from 'src/main/java'
        into commonSrcDir
        duplicatesStrategy = DuplicatesStrategy.INCLUDE

        filter {
            String line ->  line.replaceAll('@OnlyIn', '@Environment')
                    .replaceAll('Dist.CLIENT', 'EnvType.CLIENT')
                    .replaceAll('com.mlib.annotation.Dist', 'net.fabricmc.api.EnvType')
                    .replaceAll('com.mlib.annotation.OnlyIn', 'net.fabricmc.api.Environment')
        }
    }

    tasks.withType(JavaCompile) {
        dependsOn replaceFabricAnnotations
    }
}

task copyMods {
    ['client', 'server'].forEach( destination -> {
        delete "runs/${destination}/libs/"
        copy {
            from 'libs'
            into "runs/${destination}/libs"
        }
    } )
}

tasks.withType(JavaCompile) {
    dependsOn copyMods
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "com.mlib:majrusz-library-fabric:${mlib_version}"
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    compileOnly project(':common')
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : '4',
                "Implementation-Title"    : mod_display_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

loom {
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
    runs {
        client {
            client()
            setConfigName('fabric client')
            ideConfigGenerated(true)
            runDir('runs/client')
            programArg('--username=Dev')
        }
        server {
            server()
            setConfigName('fabric server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
    //accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
    options.setEncoding('UTF-8')
    options.release.set(17)
    source(commonSrcDir)
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

processResources {
    from project(':common').sourceSets.main.resources
    filesMatching('fabric.mod.json') {
        expand project.properties
    }
    outputs.upToDateWhen {
        false
    }
}

modrinth {
    token.set(System.getenv('MODRINTH_TOKEN'))
    projectId.set(modrinth_project_id)
    changelog.set(changelogText)
    versionNumber.set(mod_version)
    versionName.set("${mod_display_name} ${mod_version}")
    versionType.set('release')
    uploadFile.set(file("build/libs/${mod_archives_name}-fabric-${minecraft_version}-${mod_version}.jar"))
    gameVersions.set([minecraft_version])
    loaders.set(['fabric', 'quilt'])
    dependencies {
        required.project('majrusz-library')
    }
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    dependsOn 'build'
    group = 'publishing'
    apiToken = System.getenv('CURSEFORGE_TOKEN')

    def file = upload(curseforge_project_id, file("build/libs/${mod_archives_name}-fabric-${minecraft_version}-${mod_version}.jar"))
    file.displayName = "${mod_display_name} ${mod_version} (Fabric)"
    file.changelog = changelogText
    file.changelogType = 'markdown'
    file.addModLoader('Fabric', 'Quilt')
    file.addGameVersion(minecraft_version)
    file.addGameVersion('Client', 'Server')
    file.addJavaVersion('Java 17')
    file.releaseType = 'release'
    file.addRequirement('majrusz-library')
}

tasks.register('publishMods') {
    group = 'publishing'
    if( !mod_version.contains('unfinished') ) {
        dependsOn tasks.named('modrinth')
        dependsOn tasks.named('curseforge')
    } else {
        logger.error('Tried to publish unfinished mod')
    }
}